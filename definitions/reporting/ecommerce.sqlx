config {
  schema: `${constants.GA4_PROCESSED_DATA_DATASCHEMA}`,
  type: "incremental",
  bigquery: {
    partitionBy: 'date'
  },
  tags: ['ecommerce_events', 'ecommerce'],
}

pre_operations {
    ${ when(incremental(), `DELETE FROM ${self()} WHERE date >= DATE_SUB(CURRENT_DATE(${constants.TIME_ZONE}), INTERVAL ${constants.REFRESH_LOOKBACK_WINDOW} DAY);`)}
}

SELECT
  ${helpers.getDate()},
  FARM_FINGERPRINT(
  CONCAT(
    s.session_id,
    event_timestamp,
    event_name
  )) event_id,
  event_name,
  ${helpers.getEventParam('currency', 'string', true)},
  e.ecommerce,
  ${when(constants.EVENT_PARAMS.length),
  helpers.getEventParams(constants.EVENT_PARAMS) + ','}
  s.* EXCEPT(date)
FROM
  `${dataform.projectConfig.defaultDatabase}.${constants.GA4_DATASCHEMA}.${constants.GA4_TABLE_PREFFIX || "events_*"}` AS e
LEFT JOIN ${ref('sessions')} AS s ON ${helpers.getSessionId(false, "e")} = s.session_id
WHERE
  ${ when(incremental(),
  `_TABLE_SUFFIX >= FORMAT_DATE("%Y%m%d", DATE_SUB(CURRENT_DATE(${constants.TIME_ZONE}), INTERVAL ${constants.REFRESH_LOOKBACK_WINDOW} DAY)) AND`,
  when(constants.START_DATE, `_TABLE_SUFFIX >= FORMAT_DATE("%Y%m%d", DATE ${constants.START_DATE}) AND`)
  )}
  _TABLE_SUFFIX NOT LIKE "intraday%"
AND event_name IN (
  "add_payment_info",
  "add_shipping_info",
  "add_to_cart",
  "add_to_wishlist",
  "begin_checkout",
  "purchase",
  "refund",
  "remove_from_cart",
  "select_item",
  "select_promotion",
  "view_cart",
  "view_item",
  "view_item_list",
  "view_promotion"
)

